plugins {
	id "dev.architectury.loom" version "1.2-SNAPSHOT"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "io.github.juuxel.loom-quiltflower" version "1.10.0"
	id 'maven-publish'
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_17

if (System.getenv('VERSION') != null) {
	version = System.getenv('VERSION')
} else {
	version = "${mc_version}-${mod_version}"
}

group = "com.github.klikli_dev"
archivesBaseName = "occultism_kubejs"

loom {
	silentMojangMappingsLicense()
}

repositories {
	maven {
		url "https://maven.architectury.dev/"
	}

	maven {
		url "https://maven.saps.dev/minecraft"
		content {
			includeGroup "dev.latvian.mods"
		}
	}

	maven {
		url "https://beta.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}

	mavenLocal()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.mc_version}"
	mappings loom.officialMojangMappings()

	forge "net.minecraftforge:forge:1.19.2-43.2.14"

	modApi "dev.latvian.mods:kubejs-forge:${project.kubejs_version}"

	modCompileOnly modLocalRuntime("curse.maven:occultism-361026:4674452") // occultism-1.19.2-1.83.0.jar
	modCompileOnly modLocalRuntime("curse.maven:curios-309927:4523009") // curios (required for occultism)
	modCompileOnly modLocalRuntime("curse.maven:geckolib-388172:4407241") // geckolib (required for occultism)
	modCompileOnly modLocalRuntime("curse.maven:smartbrainlib-661293:4458565") // smartbrainlib (required for occultism)
	modCompileOnly modLocalRuntime("curse.maven:modonomicon-538392:4675974") // modonomicon-1.19.2-1.34.0.jar (required for occultism)

	// just for testing
	modRuntimeOnly "curse.maven:jei-238222:4615177"
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	def targetVersion = 17
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = targetVersion
	}
}

java {
	withSourcesJar()
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : "occultism_kubejs",
				"Specification-Vendor"    : "Kli Kli",
				"Specification-Version"   : "1", // We are version 1 of ourselves
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : "${version}",
				"Implementation-Vendor"   : "Kli Kli",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact remapJar
		}
	}
	repositories {
		maven {
			url "file:///${project.projectDir}/mcmodsrepo"
		}
	}
}
